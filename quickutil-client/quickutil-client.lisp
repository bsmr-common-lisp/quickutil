;;;; quickutil-client.lisp
;;;; Copyright (c) 2012-2013 Robert Smith

(in-package #:quickutil-client)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; Internet ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defun download-url-string (url)
  "Download the URL URL to a string."
  (nth-value 0 (drakma:http-request url)))

;; XXX FIXME: Make error handling a little more robust.
(defun download-url (url)
  "Download data from the URL URL and put it in a temporary
file. Return the pathname of the temporary file."
  (let* ((temp-stream (cl-fad:open-temporary))
         (temp (pathname temp-stream)))
    (unwind-protect (write-string (download-url-string url)
                                  temp-stream)
      (close temp-stream))
    temp))

(defun load-from-url (url)
  "Load into the Lisp image the data from the URL URL."
  (load (download-url url)))

(defun compile-and-load-from-url (url)
  "Compile and load into the Lisp image the data from the URL URL."
  (load (compile-file (download-url url))))

(defvar *quickutil-query-suffix*
  "/api/emit?"
  "The API string used to query a utility.")

(defvar *quickutil-query-argument*
  "utility=")

(defun quickutil-query-url (util-names)
  "Construct the URL from which to query for the utilities named
UTIL-NAMES."
  (assert util-names (util-names) "UTIL-NAMES must be non-null.")
  (flet ((format-argument (util-name &optional (ampersand? t))
           (when ampersand?
             (write-string "&"))
           (write-string *quickutil-query-argument*)
           (write-string (string-downcase (if (symbolp util-name)
                                              (symbol-name util-name)
                                              util-name)))))
    (with-output-to-string (*standard-output*)
      (write-string *quickutil-host*)
      (write-string *quickutil-query-suffix*)
      (format-argument (first util-names) nil)
      (mapc #'format-argument (rest util-names)))))


(defparameter *category-lookup-suffix* "/api/list/"
  "API call for finding category symbols.")

(defun category-url (category-name)
  "Construct the url for querying the symbols in the category named
CATEGORY-NAME."
  (concatenate 'string
               *quickutil-host*
               *category-lookup-suffix*
               (symbol-name category-name)))

(defun category-utilities* (category-names)
  "Query for the symbols in the categories CATEGORY-NAMES."
  (flet ((category-syms (category-name)
           (let ((str (ignore-errors (download-url-string (category-url category-name)))))
             (if (null str)
                 nil
                 (nth-value 0 (read-from-string str))))))
    (loop :for category :in category-names
          :append (category-syms category) :into symbols
          :finally (return (remove-duplicates symbols)))))

(defun category-utilities (&rest category-names)
  "Query for the symbols in the categories CATEGORY-NAMES."
  (category-utilities* category-names))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;; Public API ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;; XXX FIXME: Error when utility is not found instead of just trying
;;; to compile NIL.
(defun utilize (&rest util-names)
  "Load the utilities UTIL-NAMES and their dependencies."
  (compile-and-load-from-url (quickutil-query-url util-names)))

(defun utilize-categories (&rest category-names)
  "Load the utilities in the categories CATEGORY-NAMES."
  (apply #'utilize (category-utilities* category-names)))

(defun save-utils-as (filename &rest util-names)
  "Download the utilities listed in UTIL-NAMES to the file named
  FILENAME."
  (with-open-file (file filename :direction :output
                                 :if-exists :supersede
                                 :if-does-not-exist :create)
    (let ((file-contents (download-url-string (quickutil-query-url util-names))))
      ;; Header
      (write-string ";;;; This file was automatically generated by Quickutil." file)
      (terpri file)
      (write-string ";;;; See http://quickutil.org for details." file)
      (terpri file)
      (terpri file)
      (write-string ";;;; To regenerate:" file)
      (terpri file)
      (format file  ";;;; (qtl:save-utils-as ~S ~{~S~^ ~})~%~%"
              filename
              (mapcar #'(lambda (symb)
                          (intern (symbol-name symb) :keyword))
                      util-names))
      
      ;; Package definition
      (write-string "(eval-when (:compile-toplevel :load-toplevel :execute)" file)
      (terpri file)
      (write-string "  (unless (find-package '#:quickutil)"  file)
      (terpri file)
      (write-string "    (defpackage quickutil (:use #:cl) (:nicknames #:qtl))))" file)
      (terpri file)
      
      ;; Code
      (write-string file-contents file)
      (terpri file)
      (terpri file)
      
      ;; End of file
      (format file ";;;; END OF ~A~%" filename)
      
      ;; Return the pathname
      (pathname filename))))
